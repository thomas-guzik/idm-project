grammar idm.Qsv with org.eclipse.xtext.common.Terminals
import 'http://www.eclipse.org/emf/2002/Ecore' as ecore

generate qsv "http://www.Qsv.idm"

QuerySeparatedValues:
	header = Header
	statements += Statement*
;

Header:
	'using' nameFile=STRING 'with' 'column' 'names:' (hasColumnName?='yes'|'no')
;

Statement:
	Print
;

Print:
	'print' selector=Selector
;

Selector: // Region
	columnSelection=Columns? & lineSelection=Lines?
;

Columns:
	':columns' (columns = Column) ?
;

Column:
	ColumnNames | ColumnNumbers
;

ColumnNames:
	ids += ID (',' ids += ID)*
;

ColumnNumbers:
	numbers += INT (',' numbers += INT)*
;

Lines:
	':lines' (range=LineRange | line=Line | '*')? cond=Condition?
;

LineRange:
	start=INT '-' end=INT
;

Line:
	'#' number=INT
;

Condition:
	mid=MidPriority ('or' orCondition=Condition)?
;

MidPriority:
	high=HighestPriority ('and' andCondition=MidPriority)?
;

HighestPriority:
	baseCondition=BinCond
	| ( '(' nestedCondition=Condition ')' )
;

BinCond:
	colId=ID operator=OpComp (compStr=STRING | compId=ID | compValue=SignedInt)
;

OpComp:
	(CompareEqual | CompareNotEqual | CompareLower | CompareGreater | CompareLowerOrEqual | CompareGreaterOrEqual)
;

CompareEqual:
	{CompareEqual} ('=' | 'is')
;

CompareNotEqual:
	{CompareNotEqual} ('!=' | 'is-not')
;

CompareLower:
	{CompareLower} '<'
;

CompareGreater:
	{CompareGreater} '>'
;

CompareLowerOrEqual:
	{CompareLowerOrEqual} '<='
;

CompareGreaterOrEqual:
	{CompareGreaterOrEqual} '>='
;

Value:
	val = INT | STRING | Boolean 
;

Boolean:
	bol = 'yes'|'no'
;

Delete:
	'delete'
;

Insert:
	'insert'
;

SignedInt returns ecore::EInt: '-'? INT;



