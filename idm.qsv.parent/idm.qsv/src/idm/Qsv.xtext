grammar idm.Qsv with org.eclipse.xtext.common.Terminals
import 'http://www.eclipse.org/emf/2002/Ecore' as ecore

generate qsv "http://www.Qsv.idm"

QuerySeparatedValues:
	header = Header
	statements += Statement*
;

// TODO ? : check that column names are not used in selections if with names = no
// TODO ? : same for column insertion

Header:
	'using' nameFile=STRING 'with' 'column' 'names:' (hasColumnName?='yes'|'no')
;

Statement:
	Print | Insert | Delete | Update
;

Print:
	'print' selector=Selector
;

Delete:
	'delete' selector=Selector
;

Insert:
	'insert' inserted=Insertion
;

Update:
	'update' 
		':set' value=Value
		':columns' columns=Column
		(':lines' cond=Condition)?
;

Selector:
	columnSelection=Columns? & lineSelection=Lines?
;

Columns:
	':columns' (columns = Column) ?
;

Column:
	ColumnNames | ColumnNumbers
;

ColumnNames:
	names += ColumnName (',' names += ColumnName)*
;

ColumnNumbers:
	numbers += ColumnNumber (',' numbers += ColumnNumber)*
;

Lines:
	':lines' (range=LineRange | line=Line | '*')? cond=Condition?
;

LineRange:
	start=INT '-' end=INT
;

Line:
	'#' number=INT
;

Insertion:
	LineInsertion | ColumnInsertion
;

LineInsertion:
	':lines' rows+=ContentList (',' rows+=ContentList)*
;

ContentList:
	'(' values+=Value (',' values+=Value)* ')'
;

ColumnInsertion:
	':columns' descriptions+=ColumnDescription (',' descriptions+=ColumnDescription)*
;

ColumnDescription:
	(columnName=ColumnNameIdentifier)? content=ContentList
;

Condition:
	mid=MidPriority ('or' orCondition=Condition)?
;

MidPriority:
	high=HighestPriority ('and' andCondition=MidPriority)?
;

HighestPriority:
	baseCondition=BinCond
	| ( '(' nestedCondition=Condition ')' )
;

BinCond:
	columnId=ColumnIdentifier operator=OpComp compValue=Value
;

OpComp:
	(CompareEqual | CompareNotEqual | CompareLower | CompareGreater | CompareLowerOrEqual | CompareGreaterOrEqual)
;

CompareEqual:
	{CompareEqual} ('=' | 'is')
;

CompareNotEqual:
	{CompareNotEqual} ('!=' | 'is-not')
;

CompareLower:
	{CompareLower} '<'
;

CompareGreater:
	{CompareGreater} '>'
;

CompareLowerOrEqual:
	{CompareLowerOrEqual} '<='
;

CompareGreaterOrEqual:
	{CompareGreaterOrEqual} '>='
;

Value:
	IntegerValue | StringValue | BooleanValue 
;

IntegerValue:
	value = SignedInt
;

StringValue:
	value = STRING
;

BooleanValue:
	truthy?='yes' | {BooleanValue} 'no'
;

ColumnIdentifier:
	ColumnNumberIdentifier | ColumnNameIdentifier
;

ColumnNumberIdentifier:
	value = ColumnNumber
;

ColumnNameIdentifier:
	value = ColumnName
;

SignedInt returns ecore::EInt: '-'? INT;
ColumnNumber returns ecore::EString: '#' INT;
ColumnName returns ecore::EString: ID;


