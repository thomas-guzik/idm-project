grammar idm.Qsv with org.eclipse.xtext.common.Terminals
import 'http://www.eclipse.org/emf/2002/Ecore' as ecore

generate qsv "http://www.Qsv.idm"

QuerySeparatedValues:
	header = Header
	statements += Statement*
;

Header:
	'using' nameFile=STRING 'with' 'column' 'names:' (hasColumnName?='yes'|'no') (':csvSeparator' csvSep=STRING)?
;

Statement:
	Print | Insert | Delete | Update | Compute | Echo | Save
;

Print:
	'print' selector=Selector (format=Format)?
;

Delete:
	'delete' selector=Selector
;

Insert:
	'insert' inserted=Insertion
;

Update:
	'update' 
		':set' value=Value
		':columns' columns=Column
		(':lines' cond=Condition)?
;

Compute:
	'compute' variable=VariableIdentifier
		function=Function
;

Function:
	SumLines | SumColumns
;

SumLines:
	':sumLines' column=ColumnIdentifier
;

SumColumns:
	':sumColumns' columns+=ColumnIdentifier (',' columns+=ColumnIdentifier)*
;

Echo:
	'echo' variable=VariableIdentifier
;

Selector:
	columnSelection=Columns? & lineSelection=Lines?
;

Columns:
	{Columns} ':columns' (columns=Column)?
;

Column:
	ColumnNames | ColumnNumbers
;

ColumnNames:
	names += ColumnName (',' names += ColumnName)*
;

ColumnNumbers:
	numbers += ColumnNumber (',' numbers += ColumnNumber)*
;

Lines:
	{Lines} ':lines' (range=LineRange | line=Line | '*')? cond=Condition?
;

LineRange:
	'#' start=INT '-' end=INT
;

Line:
	'#' number=INT
;

Insertion:
	LineInsertion | ColumnInsertion
;

LineInsertion:
	':lines' rows+=ContentList (',' rows+=ContentList)*
;

ContentDescription:
	ContentList | VariableIdentifier
;

ContentList:
	'(' values+=Value (',' values+=Value)* ')'
;

ColumnInsertion:
	':columns' descriptions+=ColumnDescription (',' descriptions+=ColumnDescription)*
;

ColumnDescription:
	(columnName=ColumnNameIdentifier)? content=ContentDescription
;

Condition:
	mid=MidPriority ('or' orCondition=Condition)?
;

MidPriority:
	high=HighestPriority ('and' andCondition=MidPriority)?
;

HighestPriority:
	baseCondition=BinCond
	| ( '(' nestedCondition=Condition ')' )
;

BinCond:
	columnId=ColumnIdentifier operator=OpComp compValue=Value
;

OpComp:
	(CompareEqual | CompareNotEqual | CompareLower | CompareGreater | CompareLowerOrEqual | CompareGreaterOrEqual)
;

CompareEqual:
	{CompareEqual} ('=' | 'is')
;

CompareNotEqual:
	{CompareNotEqual} ('!=' | 'is-not')
;

CompareLower:
	{CompareLower} '<'
;

CompareGreater:
	{CompareGreater} '>'
;

CompareLowerOrEqual:
	{CompareLowerOrEqual} '<='
;

CompareGreaterOrEqual:
	{CompareGreaterOrEqual} '>='
;

Value:
	IntegerValue | StringValue | BooleanValue | VariableIdentifier
;

IntegerValue:
	value = SignedInt
;

StringValue:
	value = STRING
;

BooleanValue:
	truthy?='yes' | {BooleanValue} 'no'
;

ColumnIdentifier:
	ColumnNumberIdentifier | ColumnNameIdentifier
;

ColumnNumberIdentifier:
	value = ColumnNumber
;

ColumnNameIdentifier:
	value = ColumnName
;

VariableIdentifier:
	value = Variable
;

Save:
	'save' method=SaveMethod
;

SaveMethod:
	SaveCsv | SaveJson
;

SaveCsv:
	{SaveCsv} ':csv' (filename=STRING)?
;

SaveJson:
	{SaveJson} ':json' (filename=STRING)?
;

Format:
	PrettyFormat | SeparatorFormat
;

PrettyFormat:
	{PrettyFormat} ':pretty'
;

SeparatorFormat:
	':separator' separator=STRING
;

SignedInt returns ecore::EInt: '-'? INT;
ColumnNumber returns ecore::EString: '#' INT;
ColumnName returns ecore::EString: ID;
Variable returns ecore::EString: '$' ID;


